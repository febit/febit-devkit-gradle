plugins {
  id 'io.freefair.lombok' version '6.4.1'
  id 'com.github.hierynomus.license' version '0.16.1'
  id 'io.spring.dependency-management' version '1.0.12.RELEASE'
}

group 'org.febit.devkit.gradle'
version '1.0.0-SNAPSHOT'

ext {
  versions = [
      commonsCollections4       : '4.4',
      spotbugs                  : '4.7.1',
      commonsIo                 : '2.6',
      springBoot                : '2.7.2',
      jsr305                    : '3.0.2',
      assertj                   : '3.22.0',
      lombokPlugin              : '6.4.1',
      dependencyManagementPlugin: '1.0.12.RELEASE',
  ]
}

allprojects {

  group rootProject.group
  version rootProject.version

  repositories {
    maven { url "https://maven.aliyun.com/repository/central" }
    maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
  }
}

def javaModules() {
  subprojects.findAll {
    !it.name.endsWith('-bom')
  }
}

configure(javaModules()) {

  group rootProject.group
  version rootProject.version

  apply plugin: 'java-library'
  apply plugin: 'io.freefair.lombok'
  apply plugin: 'com.github.hierynomus.license'
  apply plugin: 'io.spring.dependency-management'

  apply from: "${rootDir}/gradle/publish-maven.gradle"

  sourceCompatibility = JavaVersion.VERSION_11
  targetCompatibility = JavaVersion.VERSION_11

  license {
    mapping {
      java = 'SLASHSTAR_STYLE'
    }
    include '**/*.java'
    strictCheck false
    skipExistingHeaders false
    header rootProject.file('etc/licenseheader.txt')
  }

  repositories {
    maven { url "https://maven.aliyun.com/repository/central" }
    maven { url 'https://maven.aliyun.com/repository/gradle-plugin' }
    maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
  }

  tasks.withType(Test) {
    testLogging.events = ["FAILED", "PASSED", "SKIPPED", "STANDARD_OUT", "STANDARD_ERROR"]
  }

  tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
    options.debug = true
    options.compilerArgs << "-Xlint:unchecked"
    options.compilerArgs << "-Xlint:deprecation"
  }

  tasks.withType(Javadoc) {
    options.encoding = 'UTF-8'
    options.addStringOption('Xdoclint:none', '-quiet')
  }

  test {
    useJUnitPlatform()
  }

  dependencies {
    compileOnly 'com.google.code.findbugs:jsr305'
    compileOnly 'com.github.spotbugs:spotbugs-annotations'

    testImplementation 'org.slf4j:slf4j-api'
    testImplementation 'org.junit.jupiter:junit-jupiter-api'
    testImplementation 'org.mockito:mockito-junit-jupiter'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
  }

  dependencyManagement {
    imports {
      mavenBom "org.springframework.boot:spring-boot-dependencies:${versions.springBoot}"
    }
    dependencies {
      dependency "com.google.code.findbugs:jsr305:${versions.jsr305}"
      dependency "org.apache.commons:commons-collections4:${versions.commonsCollections4}"
      dependency "commons-io:commons-io:${versions.commonsIo}"
      dependency "org.assertj:assertj-core:${versions.assertj}"

      dependency "io.freefair.gradle:lombok-plugin:${versions.lombokPlugin}"
      dependency "com.github.spotbugs:spotbugs-annotations:${versions.spotbugs}"
      dependency "io.spring.gradle:dependency-management-plugin:${versions.dependencyManagementPlugin}"
    }

    resolutionStrategy {
      cacheChangingModulesFor 0, 'seconds'
    }
  }
}
